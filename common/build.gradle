import groovy.io.FileType

apply plugin: 'java'

repositories {
  mavenCentral()
}

tasks.withType(JavaCompile) {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

def fixProtoPath(str) {
  def prefix = projectDir.path + "/src/main/proto/".replace("/", File.separator)
  return (str - prefix).replace(File.separator, "/")
}

task buildProto() {
  doLast {
    def protos = []
    new File(projectDir.path + "/src/main/proto").eachFileRecurse(FileType.FILES) { file ->
      protos << fixProtoPath(file.absolutePath)
    }

    javaexec {
      main = "-jar"
      args = [
         "libs/wire-compiler-2.1.2-jar-with-dependencies.jar",
         "--proto_path=src/main/proto",
         "--named_files_only",
         "--java_out=src/main/java",
      ] + protos
    }
  }
}

compileJava.dependsOn buildProto

test {
  testLogging {
    // Show that tests are run in the command-line output
    events 'passed'
  }
}

dependencies {
  implementation 'com.squareup.wire:wire-runtime:2.1.2'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'com.google.guava:guava:24.1-android'
  testCompile 'junit:junit:4.12'
}
