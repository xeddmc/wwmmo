buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.ajoberstar.grgit:grgit-core:3.1.1'
  }
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'

Properties signingProps = new Properties()
File signingPropsFile = new File("client/signing.properties")
if (signingPropsFile.isFile()) {
  signingProps.load(new FileInputStream(signingPropsFile))
}

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'

  defaultConfig {
    applicationId "au.com.codeka.warworlds2"
    minSdkVersion 19
    targetSdkVersion 28
    versionCode getVersionCodeFromGit()
    versionName "2.0"
    multiDexEnabled true
    vectorDrawables.useSupportLibrary = true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  signingConfigs {
    release {
      storeFile file(signingProps['keystore'] == null ? "/tmp/non-existant" : signingProps['keystore'])
      storePassword signingProps['password']
      keyAlias signingProps['alias']
      keyPassword signingProps['password']
    }
  }

  configurations {
    // This seems like a weird bug... something else includes listenablefuture from guava.
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release

      buildConfigField "String", "DEFAULT_SERVER", "\"http://wwmmo.codeka.com.au/\""
    }
    debug {
      buildConfigField "String", "DEFAULT_SERVER", "\"http://127.0.0.1:8080/\""
    }
  }
  packagingOptions {
    // These get added twice from different dependencies, so just ignore them.
    exclude 'LICENSE-EPL-1.0.txt'
    exclude 'LICENSE-EDL-1.0.txt'
  }
}

def deployPath = file("${rootDir}/../deploy/${project.name}")

def publish = project.tasks.create("publishAll")
def deploy = project.tasks.create("deploy")

android.applicationVariants.all { variant ->
  variant.outputs.each { output ->
    if (variant.versionName) {
      version "${variant.versionName}.${variant.versionCode}"
    } else {
      version "${variant.versionCode}"
    }

    def task = project.tasks.create("publish${variant.name.capitalize()}", Copy)
    task.from(output.outputFile)
    task.into(deployPath)
    task.rename('.*', "wwmmo2-${variant.name}-${version}.apk")
    task.dependsOn variant.assemble
    publish.dependsOn task
    if (variant.name == "release") {
      deploy.dependsOn task
    }
  }
}

def getVersionCodeFromGit() {
  // Same as running:
  // git rev-list <checked out branch name> | wc -l
  return Grgit.open(dir: project.buildscript.sourceFile.parentFile.parent).log().size()
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'com.google.android.material:material:1.1.0-alpha06'
  implementation 'androidx.preference:preference:1.1.0-alpha05'
  implementation 'com.google.android.gms:play-services-base:16.1.0'
  implementation 'com.google.firebase:firebase-core:16.0.9'
  implementation 'com.google.firebase:firebase-messaging:18.0.0'
  implementation 'com.google.guava:guava:24.1-android'
  implementation 'com.squareup.picasso:picasso:2.71828'
  implementation 'com.squareup.wire:wire-runtime:2.1.2'
  implementation 'com.android.support:multidex:1.0.3'
  implementation project(':common')
}

apply plugin: 'com.google.gms.google-services'