buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.hidetake:gradle-ssh-plugin:2.2.0'
  }
}

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.hidetake.ssh'

dependencies {
  compile 'joda-time:joda-time:2.3'
  compile 'com.google.api-client:google-api-client:1.22.0'
  compile 'com.google.api-client:google-api-client-gson:1.22.0'
  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'com.google.code.gson:gson:2.8.0'
  compile 'com.google.firebase:firebase-admin:4.1.1'
  compile 'com.google.guava:guava:21.0'
  compile 'com.squareup.wire:wire-gson-support:2.1.2'
  compile 'org.simplejavamail:simple-java-mail:4.2.1'
  compile 'org.eclipse.jetty:jetty-server:9.4.0.v20161208'
  compile 'org.eclipse.jetty.websocket:websocket-server:9.4.0.v20161208'
  compile 'org.xerial:sqlite-jdbc:3.16.1'
  compile project(':carrot')
  compile project(':common')
  compile project(':planet-render')
}

task dataFiles {
  // want to include more than just a single directory
  FileTree data = fileTree(dir: "src/main/data/")
  data.exclude "store/*"
  data.exclude "cache/*"
  outputs.dir data
}

task buildProto() {
  def protos = []
  fileTree(dir: "src/main/proto").matching { include "*.proto" }.visit {
    FileVisitDetails details ->
      protos << details.file.name
  }

  javaexec {
    main = "-jar"
    args = [
        project(':common').projectDir.getAbsolutePath() + "/libs/wire-compiler-2.1.2-jar-with-dependencies.jar",
        "--proto_path=src/main/proto",
        "--proto_path=" + project(':common').projectDir.getAbsolutePath() + "/src/main/proto",
        "--named_files_only",
        "--java_out=src/main/java",
    ] + protos
  }
}

compileJava.dependsOn buildProto

applicationDistribution.from(dataFiles) {
  into "data"
}

mainClassName = "au.com.codeka.warworlds.server.Program"

ssh.settings {
  knownHosts = file('../../known_hosts')
}

remotes {
  prod {
    host = 'wwmmo.codeka.com.au'
    user = 'wwmmo'
    identity = file('../../wwmmo2.id_rsa')
  }
}

task deploy(type: Exec, dependsOn: 'installDist') {
  workingDir "${buildDir}/install/server"
  commandLine "sh", "-c", "rsync -a -e" +
      " 'ssh -o UserKnownHostsFile=${projectDir}/../../known_hosts -i ${projectDir}/../../wwmmo2.id_rsa'" +
      " . wwmmo@wwmmo.codeka.com.au:/home/wwmmo/staging"
}
